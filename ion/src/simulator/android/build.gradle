def EPSILON_BUILD_DIR = '../../../../'+System.getenv('BUILD_DIR')
def BUILD_DIR = EPSILON_BUILD_DIR+'/app'

def projectVariable(name) {
  /* This function retrieves a variable from the environment and falls back
   * to the Gradle projects's properties (e.g. ~/.gradle/gradle.properties) */
  return System.getenv('ANDROID_'+name) ?: project.findProperty('EPSILON_'+name)
}

def fileIfPath(path) {
  return path ? new File(path) : null
}

buildscript {
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.0'
    classpath 'com.google.gms:google-services:4.3.5'
  }
}

allprojects {
  repositories {
    jcenter()
    google()
  }
  buildDir = BUILD_DIR
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion 31
  defaultConfig {
    applicationId "com.numworks.calculator"
    minSdkVersion 16
    targetSdkVersion 31
    def (major, minor, patch) = System.getenv('EPSILON_VERSION').toLowerCase().tokenize('.').collect{it.toInteger()}
    versionCode major*1000000 + minor*10000 + patch * 100
    versionName System.getenv('EPSILON_VERSION')
  }
  signingConfigs {
    environment {
      storeFile fileIfPath(projectVariable('SIGNING_STORE_FILE'))
      storePassword projectVariable('SIGNING_STORE_PASSWORD')
      keyAlias projectVariable('SIGNING_KEY_ALIAS')
      keyPassword projectVariable('SIGNING_KEY_PASSWORD')
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt')
      if (projectVariable('SIGNING_STORE_FILE')) {
        signingConfig signingConfigs.environment
      }
      ndk {
        debugSymbolLevel = 'FULL'
      }
    }
  }
  sourceSets{
    main {
      manifest.srcFile 'src/AndroidManifest.xml'
      res.srcDir BUILD_DIR + '/res'
      java.srcDir 'src'
      jniLibs.srcDir BUILD_DIR + '/libs/' + System.getenv('EPSILON_VARIANT')
      assets.srcDirs = ['../assets', EPSILON_BUILD_DIR + 'ion/src/simulator/assets']
    }
  }
  lintOptions {
    abortOnError false
    checkReleaseBuilds false
  }
  ndkVersion System.getenv('NDK_BUNDLE_VERSION')
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation "androidx.appcompat:appcompat:1.0.2"
  implementation "com.google.android.gms:play-services-analytics:18.0.1"
}
